{# This file is part of DBL, released under MIT license.
 # See LICENSE for details.
 #}


import open RedBlackTree

# Signature of Set

implicit ~onError

data Interval Value = Inclusion of Value | Exclusion of Value

pub data Set Elem = Set of {
  T
  {# @brief Creates empty set
  #}
  , empty : T
  {# @brief Method to testing whether given set is empty or not
   # @return True if it's empty false otherwise
  #}
  , method isEmpty : T -> [] Bool
  {# @brief Method for inserting element to the set
   # @param Element which will be inserted to the set
   # @return Set with inserted value
  #}
  , method insert : T -> Elem -> [] T
  {# @brief Method for removig element from the set
   # @param Element which will be removed
   # @return Set with removed element
  #}
  , method remove : T -> Elem -> [] T
  {# @brief Method to test whether given element is in a given set or not
   # @param Element which will be searched
   # @return True if given element is in given set, false otherwise
  #}
  , method member : T -> Elem -> [] Bool
  {# @brief Method to fold left through structure of set
   # @param Function that receives element and accumulator
   # @param Accumulator
   # @return Result of applying function on elements of set and accumulator
  #}
  , method foldl : {type A,E} -> T -> (Elem -> A -> [|E] A) -> A -> [|E] A
  {# @brief Method to fold right through structure of set
   # @param Function that receives element and accumulator
   # @param Accumulator
   # @return Result of applying function on elements of set and accumulator
  #}
  , method foldr : {type A,E} -> T -> (Elem -> A -> [|E] A) -> A -> [|E] A
  {# @brief Method to convert set to list of elements
  #}
  , method toList : T -> [] List Elem
  {# @brief Method to create union of two sets
   # @param Set
   # @return Union of two sets
  #}
  , method union : T -> T -> [] T
  {# @brief Method to create intersection of two sets
   # @param Set
   # @return Intersection of two sets
  #}
  , method intersection : T -> T -> [] T
  {# @brief Method to create difference of two sets
   # @param Set
   # @return Difference of two sets
  #}
  , method difference : T -> T -> [] T
  {# @brief Method to check if two sets are equal 
   # @param Set
   # @return True if two sets are equal, false otherwise.
  #}
  , method eq : T -> T -> [] Bool
  {# @brief Method to check if set which called this method is 
   # subset of a given set
   # @param Set
   # @return True if set is subset, false otherwise.
  #}
  , method subset : T -> T -> [] Bool
  {# @brief Split set to two sets one containing elements 
    # lesser then given element, second one containing equal or greater
    # @param Elem
    # @return Pair of sets with order and specification previously mentioned
  #}
  , method partionLt : T -> Elem -> [] (Pair T T)
  {# @brief Split set to two sets one containing elements 
    # lesser or equal then given element, second one containing greater
    # @param Elem
    # @return Pair of sets with order and specification previously mentioned
  #}
  , method partionGt : T -> Elem -> [] (Pair T T)
  {# @brief Method that gives a subset in a given range 
    # @param First Bool - if the subset should include lower element or not
    # @param First Elem - lower value which all elements 
    #   in returned set will be greater (or equal)
    # @param Second Bool - if the subset should include upper element or not
    # @param Second Elem - upper value which all elements in 
    #   in returned set will be lower (or equal)  
    # @return Subset in a given range
  #}
  , method range : T -> Bool -> Elem -> Bool -> Elem -> [] T
  {# @brief Method that returns lowest stored value in a set
    # @return Some value if the smallest element exist or otherwise None
  #}
  , method lowerBound : T -> [] Option Elem
  {# @brief Method that returns lowest stored value in a set
    # @return smallest element or error
  #}
  , method lowerBoundErr : 
      {Err, ~onError : Unit -> [Err] Elem} -> T -> [Err] Elem
  {# @brief Method that returns the greatest stored value in a set
    # @return Some value if the greatest element exist or otherwise None
  #}
  , method upperBound : T -> [] Option Elem
  {# @brief Method that returns the greates stored value in a set
    # @return greatest element or error
  #}
  , method upperBoundErr : 
      {Err, ~onError : Unit -> [Err] Elem} -> T -> [Err] Elem
  {#

  #}
  , method lowerBoundGt : T -> Elem -> [] Option Elem
  , method lowerBoundGtErr : 
      {Err, ~onError : Unit -> [Err] Elem} ->  T -> Elem -> [Err] Elem
  , method lowerBoundGeq : T -> Elem -> [] Option Elem
  , method lowerBoundGeqErr :
      {Err, ~onError : Unit -> [Err] Elem} ->  T -> Elem -> [Err] Elem
  , method upperBoundLt : T -> Elem -> [] Option Elem
  , method upperBoundLtErr :
      {Err, ~onError : Unit -> [Err] Elem} ->  T -> Elem -> [Err] Elem
  , method upperBoundLeq : T -> Elem -> [] Option Elem 
  , method upperBoundLeqErr :
      {Err, ~onError : Unit -> [Err] Elem} ->  T -> Elem -> [Err] Elem
}

# Red black tree implementation 

data rec Q Val = Nil | E of Val , Q Val | T of Tree Val , Q Val

let rec eqMain eq qs1 qs2 = 
  match (qs1,qs2) with
    | (Nil,Nil) => True

    | (Nil, E _ _) => False

    | (E _ _, Nil) => False

    | (T Leaf rest, _) => eqMain eq rest qs2

    | (_, T Leaf rest) => eqMain eq qs1 rest

    | (T (Node {left, value = elem, right}) rest, _) =>
        eqMain eq (T left (E elem (T right rest))) qs2
    
    | (_, T (Node {left, value = elem, right}) rest) =>
        eqMain eq qs1 (T left (E elem (T right rest)))
    
    | (E elem1 rest1, E elem2 rest2) => 
        match eq elem1 elem2 with
          | Noteq => False
          | Eq => eqMain eq rest1 rest2
        end
  end

let rec subsetMain comp qs1 qs2 = 
  match (qs1,qs2) with
    | (Nil,_) => True
    
    | (E _ _ , Nil) => False
    
    | (T Leaf rest, _) => subsetMain comp rest qs2
    
    | (_ , T Leaf rest) => subsetMain comp qs1 rest
    
    | (T (Node {left, value = elem, right}) rest, _) =>
        subsetMain comp (T left (E elem (T right rest))) qs2
    
    | (_, T (Node {left, value = elem, right}) rest) =>
        subsetMain comp qs1 (T left (E elem (T right rest)))
    
    | (E elem1 rest1, E elem2 rest2) => 
        match comp elem1 elem2 with
          | Less => False
          | Equal => subsetMain comp rest1 rest2
          | Greater => subsetMain comp qs1 rest2
        end
  end

let partionLt compare = fn tree key1 => 
  let (_,left,right) = 
    split 
      (fn key2 => 
        match compare key1 key2 with 
          | Greater => Greater 
          | _ => Less 
        end) 
      tree 
  in (left,right)

let partionGt compare = fn tree key1 => 
  let (_, left,right) =
    split 
      (fn key2 => 
        match compare key1 key2 with 
          | Less => Less 
          | _ => Greater 
        end)
      tree 
  in (left,right)

let rec least tree = 
  match tree with 
    | Leaf => None
    | Node {left = Leaf, value = x} => Some x
    | Node {left} => least left
  end

let leastErr tree =
  match least tree with
  | None => ~onError ()
  | Some x => x
  end 

let rec greatest tree = 
  match tree with 
    | Leaf => None
    | Node {value = x, right = Leaf} => Some x
    | Node {right} => greatest right
  end

let greatestErr tree = 
  match greatest tree with
  | None => ~onError ()
  | Some x => x
  end

let empty = Leaf

let isEmpty tree = 
  match tree with 
    | Leaf => True
    | _ => False
  end

let rec member compare tree elem =
  match tree with
    | Leaf => False
    | Node {left, value, right} =>
        match compare elem value with
          | Less => member compare left elem
          | Greater => member compare right elem
          | Equal => True
        end
  end
    
let insert compare tree elem = 
  match search (fn val => compare elem val) tree [] with 
    | (Leaf,zipper) => zipRed elem Leaf Leaf zipper
    | (Node ,_) => tree
  end

let remove compare tree elem = 
  match search (fn val => compare elem val) tree [] with 
    | (Leaf,_) => tree
    | (Node {color, left, right},zipper) => delete color left right zipper
  end 

let rec _search compare tree elem = 
  match tree with
  | Leaf => False
  | Node {left, value, right} => 
      match compare elem value with
        | Less => _search compare left elem
        | Greater => _search compare right elem
        | Equal => True
      end
  end

let rec setFoldl tree func acc = 
  match tree with
    | Leaf => acc
    | Node {left, value, right} => 
        setFoldl right func (func value (setFoldl left func acc))
  end

let rec setFoldr tree func acc =
  match tree with 
    | Leaf => acc
    | Node {left, value, right} =>
        setFoldr left func (func value (setFoldr right func acc))
  end

let rec toList tree acc =
  match tree with 
    | Leaf => acc
    | Node {left, value, right} => 
        toList left (value :: toList right acc)
  end

let rec union compare tree1 tree2 =
  match tree1 with
    | Leaf => tree2
    | Node {left = left1, value = key1, right = right1} => 
      match tree2 with
        | Leaf => tree1
        | Node => 
            let (_,left2,right2) = split (fn key2 => compare key1 key2) tree2 in
              joinVal (union compare left1 left2) key1 
              (union compare right1 right2)
      end
  end

let rec intersection compare tree1 tree2 =
  match tree1 with
    | Leaf => Leaf
    | Node {left = left1, value = key1, right = right1} => 
      match tree2 with
        | Leaf => Leaf
        | _ => let (value_out, left2, right2) = 
            split (fn key2 => compare key1 key2) tree2 
            in let left = intersection compare left1 left2 
            in let right = intersection compare right1 right2
            in 
            match value_out with
              | Some _ => joinVal left key1 right
              | None => join left right
            end
      end
  end
let rec difference compare tree1 tree2 = 
  match tree1 with
    | Leaf => Leaf

    | Node {left = left1, value = key1, right = right1} => 
        match tree2 with 
          | Leaf => tree1 
          | _ => 
              let (value_out, left2, right2) =
              split (fn key2 => compare key1 key2) tree2
              in let left = difference compare left1 left2
              in let right = difference compare right1 right2
              in 
              match value_out with
                | Some _ => join left right
                | None => joinVal left key1 right
              end
        end
  end

let subset compare set1 set2 = subsetMain compare (T set1 Nil) (T set2 Nil)

let _range compare tree left right = 
  match (left,right) with
    | (Inclusion left, Inclusion right) =>
        let (_, tree') = partionLt compare tree left in 
        let (tree'',_) = partionGt compare tree' right in tree''
    | (Exclusion left, Inclusion right) => 
        let (_, tree') = partionGt compare tree left in 
        let (tree'',_) = partionGt compare tree' right in tree''
    | (Inclusion left, Exclusion right) => 
        let (_, tree') = partionLt compare tree left in
        let (tree'',_) = partionLt compare tree' right in tree''
    | (Exclusion left, Exclusion right) => 
        let (_, tree') = partionGt compare tree left in
        let (tree'',_) = partionLt compare tree' right in tree''
  end

let range compare tree incl left incr right =
  match (incl,incr) with
  | (False,False) => _range compare tree (Exclusion left) (Exclusion right)
  | (False,True) => _range compare tree (Exclusion left) (Inclusion right)
  | (True,False) => _range compare tree (Inclusion left) (Exclusion right)
  | (True,True) => _range compare tree (Inclusion left) (Inclusion right)
  end
        
let rec leastGt compare tree val = 
  match tree with
  | Leaf => None
  | Node {left, value = key, right} => 
      match compare val key with
        | Less => let x = leastGt compare left val in
            match x with
              | None => Some key
              | _ => x
            end
        | Equal => least right
        | Greater => leastGt compare right val
      end
  end 

let leastGtErr compare tree val =
  match leastGt compare tree val with
  | None => ~onError ()
  | Some x => x
  end

let rec leastGeq compare tree val =
  match tree with 
  | Leaf => None
  | Node {left, value = key, right} => 
      match compare val key with
        | Less => 
            match leastGeq compare left val with 
              | None => Some key 
              | x => x 
            end
        | Equal => Some val
        | Greater => leastGeq compare right val
      end
  end

let leastGeqErr compare tree val =
  match leastGeq compare tree val with
  | None => ~onError ()
  | Some x => x
  end

let rec greatestLt compare tree val =
  match tree with 
    | Leaf => None
    | Node {left, value = key, right} => 
        match compare val key with
          | Less => greatestLt compare left val
          | Equal => greatest left
          | Greater => 
              match greatestLt compare right val with 
                | None => Some key 
                | x => x
              end
        end
  end

let greatestLtErr compare tree val =
  match greatestLt compare tree val with
  | None => ~onError ()
  | Some x => x
  end

let rec greatestLeq compare tree val =  
  match tree with
    | Leaf => None
    | Node {left, value = key, right} => 
        match compare val key with
          | Less => greatestLeq compare left val
          | Equal => Some val
          | Greater => 
              match greatestLeq compare right val with
                | None => Some key
                | x => x
              end
        end
  end

let greatestLeqErr compare tree val =
  match greatestLeq compare tree val with
  | None => ~onError ()
  | Some x => x
  end


pub let make {Val} (compare : Val -> Val -> [] Ordered) = Set {
  T = Tree Val
  , empty = empty
  , method isEmpty = isEmpty
  , method insert = insert compare
  , method remove = remove compare
  , method member = member compare
  , method foldl = setFoldl
  , method foldr = setFoldr
  , method toList = fn tree => toList tree []
  , method union = union compare
  , method intersection = intersection compare
  , method difference = difference compare
  , method eq = fn set1 set2 => 
      eqMain (fn e1 e2 => (compare e1 e2).toComparable) (T set1 Nil) (T set2 Nil)
  , method subset = subset compare
  , method partionLt = partionLt compare
  , method partionGt = partionGt compare
  , method range = range compare
  , method lowerBound = least
  , method lowerBoundErr = leastErr
  , method upperBound = greatest
  , method upperBoundErr = greatestErr
  , method lowerBoundGt = leastGt compare
  , method lowerBoundGtErr = leastGtErr compare
  , method lowerBoundGeq = leastGeq compare
  , method lowerBoundGeqErr = leastGeqErr compare
  , method upperBoundLt = greatestLt compare
  , method upperBoundLtErr = greatestLtErr compare
  , method upperBoundLeq = greatestLeq compare
  , method upperBoundLeqErr = greatestLeqErr compare
}
